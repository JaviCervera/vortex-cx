Strict

#GLFW_WINDOW_TITLE="Vortex2 Material Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Set skybox
		World.SkyboxTexture(Texture.Load("left.jpg", "right.jpg", "front.jpg", "back.jpg", "top.jpg", "bottom.jpg"))
		
		'Create camera
		mCam = New Camera()
		mCam.ClearMode = Camera.CLEAR_SKYBOX
		
		'Create box
		mModel = New Model(Mesh.Load("cube.msh.dat"))
		
		'Create light attached to camera
		mLight = New Light(mCam)
		mLight.Radius = 10
		
		'Disable sun and setup lighting
		World.SunColor(Color.BLACK)
		World.Ambient(Color.WHITE)
			
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Rotate camera
		mCam.Yaw += 32 * World.DeltaTime()
		mCam.Position(0, 0, 0)
		mCam.Move(0, 0, -3)
		
		'Update shininess
		If KeyDown(KEY_LEFT) Then mModel.Material.Shininess = Clamp(mModel.Material.Shininess - 0.2 * World.DeltaTime(), 0.0, 1.0)
		If KeyDown(KEY_RIGHT) Then mModel.Material.Shininess = Clamp(mModel.Material.Shininess + 0.2 * World.DeltaTime(), 0.0, 1.0)

		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Graphics.Color(Color.WHITE)
		Local text:String = Graphics.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Graphics.RenderCalls()
		mFont.Draw(2, 18, text)
		
		'Draw shininess
		text = "Shininess: " + String(mModel.Material.Shininess)[..4] + " (LEFT and RIGHT to change)"
		mFont.Draw(2, 34, text)
	
		Return False
	End
Private
	Field mFont		: Font
	Field mCam		: Camera
	Field mModel	: Model
	Field mLight	: Light
End

Function Main:Int()
	New TestApp()
	Return False
End