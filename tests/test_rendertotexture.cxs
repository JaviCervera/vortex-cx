Import mojo.app
Import vortex

Class TestApp Extends App
	Method OnCreate:Int()
		'Init Vortex
		If Not World.Init()
			'Print Graphics.ShaderError()
			EndApp()
		End
		
		'Create camera
		mCam = New Camera()
		mCam.Position(0, 0, -3)
		mCam.BackColor = Color.DarkGray
		
		'Create framebuffer without depth buffer
		mFramebuffer = New Framebuffer(512, 512, False)
		
		'Create cube
		mCube = New Model(Mesh.CreateCube())
		mCube.Material(0).ColorTexture = mFramebuffer.ColorTexture
		
		'Create light
		Local light:Light = New Light()
		light.Rotate(45, -45, 0)
	End
	
	Method OnUpdate:Int()
		'Update world
		World.Update()

		'Rotate cube
		mCube.Turn(32 * World.DeltaTime(), 32 * World.DeltaTime(), 0)
	End
	
	Method OnRender:Int()
		'Render to framebuffer
		mFramebuffer.Use()
		
		'Draw 2D stuff
		Graphics.Setup2D(0, 0, 512, 512)
		Graphics.Clear(Color.White)
		Graphics.Color(Color.Orange)
		Graphics.DrawRect(Rnd(0, 512), Rnd(0, 512), Rnd(0, 512), Rnd(0, 512))
		Graphics.Color(Color.Brown)
		Graphics.DrawRect(Rnd(0, 512), Rnd(0, 512), Rnd(0, 512), Rnd(0, 512))
		Graphics.Color(Color.Green)
		Graphics.DrawEllipse(Rnd(0, 512), Rnd(0, 512), Rnd(0, 512), Rnd(0, 512))
		
		'Render back to screen
		Framebuffer.UseScreen()
	
		'Render world
		World.Render()
	End
Private
	Field mCam			: Camera
	Field mFramebuffer	: Framebuffer
	Field mCube			: Model
End

Function Main:Int()
	New TestApp
	Return False
End
