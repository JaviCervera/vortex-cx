Strict

#GLFW_WINDOW_TITLE="Vortex2 Listener Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.audio
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Load sound and get available channel
		mSound = LoadSound("heli.wav")
		mChannel = Listener.AvailableChannel()
		
		'Set skybox
		World.SkyboxTexture(Texture.Load("left.jpg", "right.jpg", "front.jpg", "back.jpg", "top.jpg", "bottom.jpg"))
		
		'Create camera
		mCam = New Camera()
		mCam.ClearMode = Camera.CLEAR_SKYBOX
		mCam.Position(0, 2, 0)
		mCam.CollisionMode = Entity.COLLISION_SPHERE
		mCam.Radius = 2
		
		'Create mesh
		Local mesh:Mesh = Mesh.CreateCube()
		
		'Create floor
		Local floor:Model = New Model(mesh)
		floor.Position(0, -0.5, 0)
		floor.Scale(50, 1, 50)
		floor.Material.Color = Color.BLACK
		floor.Material.Shininess = 0.1
		
		'Create pillars
		Local xpos:Int[] = [-10, -10, 10, 10]
		local zpos:Int[] = [-10, 10, -10, 10]
		For Local i:Int = 0 Until xpos.Length
			Local pillar:Model = New Model(mesh)
			pillar.Position(xpos[i], 4, zpos[i])
			pillar.Scale(4, 8, 4)
			pillar.Material.Color = Color.LIGHT_GRAY
			pillar.Material.Shininess = 0.001
			pillar.CollisionMode = Entity.COLLISION_BOX
		Next
		
		'Create floating cube
		mModel = New Model(mesh)
		mModel.Material.Color = Color.BLUE
		mModel.Material.Shininess = 0.001
		
		'Setup lighting
		World.SunRotation(45, 215)
		World.Shadows(True, 35, 100)
		World.Ambient(Color.RGB(155, 155, 255))
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera view
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Rotate and move camera
		If KeyDown(KEY_LEFT) Then mCam.Yaw -= 64 * World.DeltaTime()
		If KeyDown(KEY_RIGHT) Then mCam.Yaw += 64 * World.DeltaTime()
		If KeyDown(KEY_UP) Then mCam.Move(0, 0, 32 * World.DeltaTime())
		If KeyDown(KEY_DOWN) Then mCam.Move(0, 0, -32 * World.DeltaTime())
		
		'Rotate cube
		mModel.Position(0, 3, 0)
		mModel.Yaw += 8 * World.DeltaTime()
		mModel.Move(0, 0, -20)
		
		'Emit sound
		If ChannelState(mChannel) = 0 Or ChannelState(mChannel) = -1
			mModel.EmitSound(mSound, 25, mChannel)
		End

		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Graphics.Color(Color.WHITE)
		Local text:String = Graphics.FPS() + " FPS"
		mFont.Draw(2, 2, text)
	
		Return False
	End
Private
	Field mFont			: Font
	Field mCam			: Camera
	Field mModel		: Model
	Field mLight		: Light
	Field mSound		: Sound
	Field mChannel	: Int
End

Function Main:Int()
	New TestApp()
	Return False
End