Strict

#GLFW_WINDOW_TITLE="Vortex2 Lighting Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Load sphere mesh
		Local mesh:Mesh = Mesh.Load("sphere.msh.dat")
		
		'Create camera
		mCam = New Camera()
		mCam.BackgroundColor = Color.Black
		mCam.Position(0, 32, -90)
		mCam.Rotate(16, 0, 0)
		
		'Create sphere models
		Local x:Int = -32, z:Int = -32
		For Local i:Int = 0 Until 81
			Local model:Model = New Model(mesh)
			model.Position(x, 0, z)
			x += 8; If x > 32 Then x = -32; z += 8
		Next
		
		'Create lights
		mLights[0] = New Light
		mLights[1] = New Light
		mLights[2] = New Light
		mLights[0].Radius = 48
		mLights[1].Radius = 48
		mLights[2].Radius = 48
		mLights[0].Color = Color.Red
		mLights[1].Color = Color.Green
		mLights[2].Color = Color.Blue
		mLights[0].Yaw = 0
		mLights[1].Yaw = 120
		mLights[2].Yaw = 240
		
		'Disable sun and set ambient
		World.SunColor(Color.Black)
		World.Ambient(Color.Black)
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update lights
		For Local i:Int = 0 Until 3
			mLights[i].Yaw -= 32 * World.DeltaTime()
			mLights[i].Position(0, 0, 0)
			mLights[i].Move(0, 0, -48)
		Next
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Graphics.Color(Color.White)
		Local text:String = Graphics.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Graphics.RenderCalls()
		mFont.Draw(2, 18, text)
	
		Return False
	End
Private
	Field mFont		: Font
	Field mCam		: Camera
	Field mLights	: Light[3]
End

Function Main:Int()
	New TestApp()
	Return False
End
