Strict

#GLFW_WINDOW_TITLE="Vortex2 Primitives Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Const PRIM_POINT:Int = 0
Const PRIM_LINE:Int = 1
Const PRIM_RECT:Int = 2
Const PRIM_ELLIPSE:Int = 3

Class Prim Final
	Field type:Int
	Field x:Float, y:Float, z:Float, w:Float
	Field color:Int
End

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Init primitives
		mNumPrimitives = 0
		For Local i:Int = 0 Until mPrimitives.Length()
			mPrimitives[i] = New Prim
		Next
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()

		'Add a new primitive up until a max of 1000
		If mNumPrimitives < mPrimitives.Length()
			mNumPrimitives += 1
			mPrimitives[mNumPrimitives-1].type = Int(Rnd(0, 4))
			mPrimitives[mNumPrimitives-1].x = Rnd(DeviceWidth())
			mPrimitives[mNumPrimitives-1].y = Rnd(DeviceHeight())
			mPrimitives[mNumPrimitives-1].z = Rnd(DeviceWidth())
			mPrimitives[mNumPrimitives-1].w = Rnd(DeviceHeight())
			mPrimitives[mNumPrimitives-1].color = Color.RGB(Rnd(256), Rnd(256), Rnd(256))
		End
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world (it actually renders nothing here, but sets the screen fps)
		World.Render()
		
		'Setup graphics for 2D graphics
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		Graphics.Clear()
		For Local i:Int = 0 Until mNumPrimitives
			Local prim:Prim = mPrimitives[i]
			Graphics.Color(prim.color)
			Select prim.type
				Case PRIM_POINT
					Graphics.DrawPoint(prim.x, prim.y)
				Case PRIM_LINE
					Graphics.DrawLine(prim.x, prim.y, prim.z, prim.w)
				Case PRIM_RECT
					Graphics.DrawRect(prim.x, prim.y, prim.z, prim.w)
				Case PRIM_ELLIPSE
					Graphics.DrawEllipse(prim.x, prim.y, prim.z, prim.w)
			End
		Next
		
		'Draw FPS
		Graphics.Color(Color.White)
		Local text:String = Graphics.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Num primitives: " + mNumPrimitives
		mFont.Draw(2, 18, text)
	
		Return False
	End
Private
	Field mFont				: Font
	Field mNumPrimitives	: Int
	Field mPrimitives		: Prim[1000]
End

Function Main:Int()
	New TestApp()
	Return False
End
