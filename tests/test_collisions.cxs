Strict

#GLFW_WINDOW_TITLE="Vortex2 Collisions Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Setup world
		World.Ambient(Color.LightGray)
		ToggleShadows()
		
		'Create sphere
		mBall = New Model(Mesh.Load("sphere.msh.dat"))
		mBall.Position(3.5, 0.5, 2.5)
		mBall.Scale(0.5, 0.5, 0.5)
		mBall.CollisionMode = Entity.CollisionSphere
		mBall.Radius = 0.5
		mBall.Material.Color = Color.Red
		
		'Create camera
		mCam = New Camera(mBall)
		mCam.Position(0, 14, -16)
		mCam.Rotate(45, 0, 0)
		mCam.BackColor = Color.RGB(15, 25, 55)
		
		'Create level
		CreateLevel()
		
		'Setup lighting
		World.SunRotation(15, 60)

		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update ball
		If KeyDown(KEY_UP) Then mBall.Move(0, 0, 2 * World.DeltaTime())
		If KeyDown(KEY_DOWN) Then mBall.Move(0, 0, -2 * World.DeltaTime())
		If KeyDown(KEY_LEFT) Then mBall.Move(-2 * World.DeltaTime(), 0, 0)
		If KeyDown(KEY_RIGHT) Then mBall.Move(2 * World.DeltaTime(), 0, 0)
		
		'Update camera yaw
		mCam.Yaw = (mBall.WorldX - 6.5) / 6.5 * -15
		
		'Update door
		mDoor.Move(0, 2 * World.DeltaTime() * mDoorDir, 0)
		If mDoor.Y >= 2 Then mDoor.Y = 2; mDoorDir *= -1
		If mDoor.Y <= 0 Then mDoor.Y = 0; mDoorDir *= -1
		If mDoor.Collided Then mDoorDir *= -1
		
		'Update block ball
		mBlockBall.Move(0, 2 * World.DeltaTime() * mBlockBallDir, 0)
		If mBlockBall.Y >= 4 Then mBlockBall.Y = 4; mBlockBallDir *= -1
		If mBlockBall.Y <= 0 Then mBlockBall.Y = 0; mBlockBallDir *= -1
		If mBlockBall.Collided Then mBlockBallDir *= -1
		
		'Toggle shadows
		If KeyHit(KEY_SPACE) Then ToggleShadows()
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		Graphics.Color(Color.White)
		
		'Draw FPS
		Local text:String = Graphics.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Graphics.RenderCalls()
		mFont.Draw(2, 18, text)
		
		'Draw shadows message
		text = "Press SPACE to toggle shadows"
		mFont.Draw((DeviceWidth() - mFont.TextWidth(text)) / 2, DeviceHeight() - mFont.TextHeight(text) - 8, text)
		
		'World.DepthTexture().Draw(4, 4, 300, -225)
		
		Return False
	End
	
	Method CreateLevel:Void()
		'Create cube mesh
		Local mesh:Mesh = Mesh.CreateCube()
		mesh.Material(0).Color = Color.Orange
		mesh.Translate(0.5, 0.5, 0.5)
		
		'Create south wall
		Local model:Model = New Model(mesh)
		model.CollisionMode = Entity.CollisionBox
		model.Scale(13, 1, 1)
		
		'Create north wall
		model = New Model(mesh)
		model.CollisionMode = Entity.CollisionBox
		model.Position(0, 0, 13)
		model.Scale(13, 1, 1)
		
		'Create west wall
		model = New Model(mesh)
		model.CollisionMode = Entity.CollisionBox
		model.Position(0, 0, 1)
		model.Scale(1, 1, 12)	
		
		'Create east wall
		model = New Model(mesh)
		model.CollisionMode = Entity.CollisionBox
		model.Position(12, 0, 1)
		model.Scale(1, 1, 12)
		
		'Create middle wall
		model = New Model(mesh)
		model.CollisionMode = Entity.CollisionBox
		model.Position(6, 0, 1)
		model.Scale(1, 1, 8)
		
		'Create floor
		model = New Model(mesh)
		model.CollisionMode = Entity.CollisionBox
		model.Position(0, -1, 0)
		model.Scale(13, 1, 13)
		model.Material.Color = Color.Brown
		
		'Create door
		mDoor = New Model(mesh)
		mDoor.CollisionMode = Entity.CollisionBox
		mDoor.Position(6, 0, 9)
		mDoor.Scale(1, 1, 4)
		mDoor.Material.Color = Color.Cyan
		mDoorDir = 1
		
		'Create first blocking sphere
		model = New Model(Mesh.Load("sphere.msh.dat"))
		model.CollisionMode = Entity.CollisionSphere
		model.Radius = 1.2
		model.Position(9, 1, 10)
		model.Material.Color = Color.Yellow
		
		'Create second blocking sphere
		mBlockBall = New Model(Mesh.Load("sphere.msh.dat"))
		mBlockBall.CollisionMode = Entity.CollisionSphere
		mBlockBall.Radius = 1.2
		mBlockBall.Position(9, 3, 5)
		mBlockBall.Material.Color = Color.Magenta
		mBlockBallDir = -1
	End
	
	Method ToggleShadows:Void()
		If World.Shadows()
			World.Shadows(False)
		Else
			World.Shadows(True, 50, 0, 2048)
		End
	End
Private
	Field mFont			: Font
	Field mCam			: Camera
	Field mBall			: Model
	Field mDoor			: Model
	Field mDoorDir		: Float
	Field mBlockBall	: Model
	Field mBlockBallDir	: Float
	Field mLight		: Light
End

Function Main:Int()
	New TestApp()
	Return False
End
