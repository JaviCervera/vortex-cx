Strict

#GLFW_WINDOW_TITLE="Vortex2 Triangle Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Create camera
		mCam = New Camera()
		mCam.ClearColor = Color.WHITE
		mCam.Position(0, 0, -2)
		
		'Create triangle mesh
		Local tri:Mesh = Mesh.CreateTriangle()
		tri.Surface(0).VertexColor(0, Color.RED)
		tri.Surface(0).VertexColor(1, Color.GREEN)
		tri.Surface(0).VertexColor(2, Color.BLUE)
		tri.Rebuild() 'After modifying a mesh, we must rebuild it
		tri.Material(0).Culling = False
		
		'Create triangle model
		mModel = New Model(tri)
		
		'Disable sun
		World.SunColor(Color.BLACK)
		
		Return False
	End
	
	Method OnUpdate:Int()
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera view
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Rotate triangle
		mModel.Yaw += 64 * World.DeltaTime()
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Graphics.Color(Color.BLACK)
		Local text:String = Graphics.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Graphics.RenderCalls()
		mFont.Draw(2, 18, text)
	
		Return False
	End
Private
	Field mFont				: Font
	Field mCam				: Camera
	Field mModel			: Model
End

Function Main:Int()
	New TestApp
	Return False
End
