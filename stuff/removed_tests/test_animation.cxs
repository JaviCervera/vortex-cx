'NOTE: Do not use skinned animation! It has some bugs that need to be fixed.
'Check  the vertexanimation test to see how to use animation in the current version.

Strict

#GLFW_WINDOW_TITLE="Vortex2 Animation Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("system.fnt.dat")
		
		'Create camera
		mCam = New Camera()
		mCam.Position(0, 64, -128)
		mCam.Rotate(10, 0, 0)
		mCam.ClearColor = Color.RGB(15, 15, 15)
		
		'Load swat model
		mSwatModel = New Model(Mesh.Load("swat.msh.dat", "swat.skl.dat"))
		mSwatModel.Mesh.LoadAnimation("swat.anm.dat", "main")
		mSwatModel.AnimSequence = "main"
		mSwatModel.Position(-32, 32, 0)
		mSwatModel.Rotate(0, -15, 0)
		mSwatModel.Scale(7, 7, 7)
		
		'Load dwarf model
		mDwarfModel = New Model(Mesh.Load("dwarf.msh.dat", "dwarf.skl.dat"))
		mDwarfModel.Mesh.LoadAnimation("dwarf.anm.dat", "main")
		mDwarfModel.AnimSequence = "main"
		mDwarfModel.Position(32, 0, 0)
		mDwarfModel.Rotate(0, 15, 0)
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera view
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		Graphics.Color(Color.White)
		'mFont.Draw(4, DeviceHeight() - 20, "Time: " + Int(mDwarfModel.AnimTime))
		
		'Draw FPS
		Local text:String = Graphics.Fps() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Graphics.RenderCalls()
		mFont.Draw(2, 18, text)
	
		Return False
	End
Private	
	Field mFont			: Font
	Field mCam			: Camera
	Field mSwatModel	: Model
	Field mDwarfModel	: Model
End

Function Main:Int()
	New TestApp()
	Return False
End
