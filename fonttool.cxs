'NOTE: To build on Win32 GCC, you need to go to the Makefile (i.e. glfw3/gcc_winnt/Makefile)
'and add -lole32 to the LDLIBS property

Strict

'Config settings
#GLFW_WINDOW_TITLE="Vortex2 Font Tool"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2
#GLFW_GCC_MSIZE_WINNT="32"
#If HOST = "winnt"
#BINARY_FILES += "*.exe"
#End

'Ensure 32 bit Windows builds
#If HOST = "winnt"
#CC_OPTS="-m32"
#End

'Imports
Import mojo.app
Import mojo.input
Import src_tools.fonttool_gui
Import vortex

Class FontToolApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(30)
		SetSwapInterval(1)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Create gui
		mGui = New Gui
		
		Return False
	End
	
	Method OnUpdate:Int()
		'Update world
		World.Update()
	
		'Update GUI
		Local newFont:Font = mGui.Update(mFont)
		If newFont <> Null
			If mFont Then mFont.Discard()
			mFont = newFont
		End
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		mGui.Render(mFont)
		
		Return False
	End
Private
	Field mFont	: Font
	Field mGui	: Gui
End

Function Main:Int()
	New FontToolApp()
	Return False
End
