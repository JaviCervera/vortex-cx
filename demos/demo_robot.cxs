Strict

#GLFW_WINDOW_TITLE="Vortex2 Robot"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2
'#GLFW_GCC_MSIZE_WINNT="32"

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		mShaderTypeNames = ["MINIMAL", "MINIMAL WITH SHADOWS", "VERTEX LIGHTING", "PIXEL LIGHTING"]
	
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Create camera
		mCam = New Camera()
		mCam.Position(16, 16, -16)
		mCam.Rotate(20, -45, 0)
		mCam.BackColor = Color.RGB(15, 15, 15)
		
		'Create robot model
		mModel = New Model(Mesh.Load("robot.msh.dat"))
		'mModel.Yaw = 180.0
		
		'Create floor mesh
		Local mesh:Mesh = Mesh.CreateQuad()
		mesh.Rotate(90, 0, 0)
		mesh.Scale(150, 150, 150)
		
		'Create floor model
		mFloor = New Model(mesh)
		'mFloor.Material(0).Lighting = False
		mFloor.Material(0).Color = Color.RGB(155, 155, 155)
		
		'Create lights
		mLights[0] = New Light
		'mLights[0].Rotate(45, -45, 0)
		mLights[0].Position(32, 0, 32)
		mLights[0].Color = Color.RGB(125, 161, 191)
		mLights[1] = New Light
		mLights[1].Position(-32, 32, -32)
		mLights[1].Color = Color.RGB(255, 100, 0)
		
		'Disable sun & enable shadows (sun rotation is used only to cast shadows)
		World.SunColor(Color.Black)
		World.SunRotation(30, 0)
		World.Shadows(True, 50)

		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Change default shader type with mouse click or touch input
		If TouchHit(0) Then Shader.DefaultType((Shader.DefaultType() + 1) Mod 4)
		
		'Update world
		World.Update()
		
		'Update model
		mModel.Yaw += 32 * World.DeltaTime()
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw mesh credits
		Local text:String = "Robo_OBJ_pose4 by Artem Shupa-Dubrova is licensed under CC Attribution-NoDerivs"
		Graphics.Color(Color.RGB(240, 240, 240))
		mFont.Draw((DeviceWidth() - mFont.TextWidth(text)) / 2, 8, text)
		
		'Draw shader type
		Graphics.Color(Color.RGB(255, 100, 0))
		text = "Current shader: " + mShaderTypeNames[Shader.DefaultType()] + " (touch screen to change)"
		mFont.Draw((DeviceWidth() - mFont.TextWidth(text)) / 2, DeviceHeight() - mFont.TextHeight(text) - 8, text)
		
		'Draw FPS
		Graphics.Color(Color.RGB(125, 161, 191))
		text = Graphics.FPS() + " FPS"
		mFont.Draw(8, 8, text)
	
		Return False
	End
Private
	Field mShaderTypeNames	: String[4]
	Field mFont							: Font
	Field mCam							: Camera
	Field mModel						: Model
	Field mFloor						: Model
	Field mLights						: Light[2]
	Field mValue						: Float
End

Function Main:Int()
	New TestApp()
	Return False
End
