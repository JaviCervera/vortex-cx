Strict

#GLFW_WINDOW_TITLE="Vortex2 Mars One"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Const ROT_SPEED:Int = 32
Const MOVE_SPEED:Int = 256

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Create camera
		mCam = New Camera()
		mCam.BackgroundColor = Color.RGB(15, 15, 15)
		mCam.Position(0, 50, 150)
		mCam.Yaw = 180
		mCam.Far = 5000
		
		'Create model
		mModel = New Model(Mesh.Load("mars_one_base.msh.dat"))
		
		'Disable sun and enable fog
		World.SunColor(Color.Black)
		World.FogEnabled(True)
		World.FogColor(Color.RGB(15, 15, 15))
		World.FogMinDistance(1000)
		World.FogMaxDistance(5000)
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		If KeyDown(KEY_Q) Then mCam.Move(0, MOVE_SPEED * World.DeltaTime(), 0)
		If KeyDown(KEY_A) Then mCam.Move(0, -MOVE_SPEED * World.DeltaTime(), 0)
		
		If KeyDown(KEY_W) Then mCam.Turn(-ROT_SPEED * World.DeltaTime(), 0, 0)
		If KeyDown(KEY_S) Then mCam.Turn(ROT_SPEED * World.DeltaTime(), 0, 0)
		
		If KeyDown(KEY_UP) Then mCam.Move(0, 0, MOVE_SPEED * World.DeltaTime())
		If KeyDown(KEY_DOWN) Then mCam.Move(0, 0, -MOVE_SPEED * World.DeltaTime())
		If KeyDown(KEY_SHIFT)
			If KeyDown(KEY_LEFT) Then mCam.Move(-MOVE_SPEED * World.DeltaTime(), 0, 0)
			If KeyDown(KEY_RIGHT) Then mCam.Move(MOVE_SPEED * World.DeltaTime(), 0, 0)
		Else
			If KeyDown(KEY_LEFT) Then mCam.Turn(0, -ROT_SPEED * World.DeltaTime(), 0)
			If KeyDown(KEY_RIGHT) Then mCam.Turn(0, ROT_SPEED * World.DeltaTime(), 0)
		End
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Graphics.Color(Color.White)
		Local text:String = Graphics.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Graphics.RenderCalls()
		mFont.Draw(2, 18, text)
		
		'Draw mesh attribution
		text = "~qMars One~q Mission - Base by admone is licensed under CC Attribution"
		mFont.Draw((DeviceWidth() - mFont.TextWidth(text)) / 2, 2, text)
	
		Return False
	End
Private
	Field mFont		: Font
	Field mCam		: Camera
	Field mModel	: Model
End

Function Main:Int()
	New TestApp()
	Return False
End
