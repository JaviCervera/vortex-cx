Strict

#GLFW_WINDOW_TITLE="Vortex2 Van Gogh"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2
'#GLFW_GCC_MSIZE_WINNT="32"

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Graphics.VendorName()
		Print "Renderer name: " + Graphics.RendererName()
		Print "API version name: " + Graphics.APIVersionName()
		Print "Shading version name: " + Graphics.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("Roboto-Black_16.fnt.dat")
		
		'Create camera
		mCam = New Camera()
		mCam.Position(0, 1.5, -6)
		
		'Create model
		mModel = New Model(Mesh.Load("vangogh.msh.dat"))
		mModel.Yaw = 180
		
		'Disable sun
		World.SunColor(Color.BLACK)
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera view
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Update camera rotation
		mValue += 0.5 * World.DeltaTime()
		mCam.Yaw = 30 * Sinr(mValue)
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup graphics for 2D
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw
		Local text:String = "Van Gogh Room by ruslans3d is licensed under CC Attribution"
		Graphics.Color(Color.RGB(15, 15, 15, 200))
		Graphics.DrawRect(0, 8, DeviceWidth(), 20)
		Graphics.Color(Color.RGB(100, 120, 153))
		mFont.Draw((DeviceWidth() - mFont.TextWidth(text)) / 2, 8, text)
	
		Return False
	End
Private	
	Field mFont				: Font
	Field mCam				: Camera
	Field mModel			: Model
	Field mValue			: Float
End

Function Main:Int()
	New TestApp()
	Return False
End
