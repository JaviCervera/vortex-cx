Strict

Import vortex

Class Rect
	Field x:Float
	Field y:Float
	Field width:Float
	Field height:Float
	Method New(x:Float, y:Float, w:Float, h:Float)
		Self.x = x
		Self.y = y
		Self.width = w
		Self.height = h
	End
	
	Method IsPointInside:Bool(x:Float, y:Float)
		If InRange(x, Self.x, Self.x+width) And InRange(y, Self.y, Self.y+height) Then Return True Else Return False
	End
	
	Method InRange:Bool(val:Float, min:Float, max:Float)
		If val >= min And val <= max Then Return True Else Return False
	End
	
	Method Set:Void(other:Rect)
		x = other.x
		y = other.y
		width = other.width
		height = other.height
	End
End

Function DrawRectOutline:Void(r:Rect)
	DrawRectOutline(r.x, r.y, r.width, r.height)
End

Function DrawRectOutline:Void(x:Float, y:Float, width:Float, height:Float)
	Graphics.Color(Color.DarkGray)
	Graphics.DrawLine(x-1, y, x+width, y)
	Graphics.DrawLine(x, y+height, x+width, y+height)
	Graphics.DrawLine(x, y, x, y+height)
	Graphics.DrawLine(x+width, y, x+width, y+height)
End

Function DrawPanel:Void(r:Rect, text:String = "", font:Font = Null, color:Int = Color.LightGray, textColor:Int = Color.DarkGray)
	DrawPanel(r.x, r.y, r.width, r.height, text, font, color, textColor)
End

Function DrawPanel:Void(x:Float, y:Float, width:Float, height:Float, text:String = "", font:Font = Null, color:Int = Color.LightGray, textColor:Int = Color.DarkGray)
	Graphics.Color(color)
	Graphics.DrawRect(x, y, width, height)
	DrawRectOutline(x, y, width, height)
	If text <> "" And font <> Null
		Graphics.Color(textColor)
		font.Draw(x + width/2 - font.TextWidth(text)/2, y + height/2 - font.TextHeight(text)/2, text)
	End
End

Function DrawCheckbox:Void(r:Rect, text:String, font:Font, checked:Bool)
	DrawCheckbox(r.x, r.y, r.width, r.height, text, font, checked)
End

Function DrawCheckbox:Void(x:Float, y:Float, width:Float, height:Float, text:String, font:Font, checked:Bool)
	DrawPanel(x, y, width, height)
	DrawRectOutline(x + 4, y + 4, 16, 16)
	If checked
		Graphics.DrawLine(x + 4, y + 4, x + 20, y + 20)
		Graphics.DrawLine(x + 4, y + 20, x + 20, y + 4)
	End
	Graphics.Color(Color.DarkGray)
	font.Draw(x + 24, y + height/2 - font.TextHeight(text)/2, text)
End